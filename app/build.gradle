apply plugin: 'com.android.application'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.0"
    defaultConfig {
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [eventBusIndex: 'ac.shenkar.workshoptwo.MyEventBusIndex']
            }
        }
        applicationId "ac.shenkar.workshoptwo"
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    dataBinding {
        enabled = true
    }
}


dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.3-alpha', {
        exclude group: 'com.android.support', module: 'support-annotations'
        // resolves a version collision with espresso
        exclude group: 'com.google.code.findbugs'
    })

    // list of firebase libraries: https://firebase.google.com/docs/android/setup#available_libraries
    // firebase analytics
    compile 'com.google.firebase:firebase-core:11.0.1'
    // firebase Realtime Database
    compile "com.google.firebase:firebase-database:11.0.1"

    // ignore the warning due to different appcompat inside firebase ui library
    //noinspection GradleCompatible
    // latest version: https://developer.android.com/topic/libraries/support-library/packages.html
    compile 'com.android.support:appcompat-v7:26.0.0-alpha1'
    compile "com.android.support.constraint:constraint-layout:1.0.2"
    compile 'com.android.support:support-v4:26.0.0-alpha1'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.8.9'

    // the two lines below are for EventBus by http://greenrobot.org/eventbus
    compile 'org.greenrobot:eventbus:3.0.0'
    annotationProcessor 'org.greenrobot:eventbus-annotation-processor:3.0.1'

    // https://github.com/firebase/FirebaseUI-Android
    // as instructed on: https://github.com/firebase/FirebaseUI-Android/tree/master/auth
    // FirebaseUI Auth
    compile "com.firebaseui:firebase-ui-auth:1.2.0"

    // problem: firebaseui has a dependency on old version of firebase-auth
    // so I override the above's library dependency version:
    compile "com.google.firebase:firebase-auth:11.0.1"

    // FCM: messaging
    // https://firebase.google.com/docs/cloud-messaging/android/client
    compile 'com.google.firebase:firebase-messaging:11.0.1'

    /**
     * Note that in Dagger version 2.11 all HasDispatching*Injectors interfaces
     * have been renamed to Has*Injectors
     */
    ext.daggerVersion = "2.11"

    // Dependency Injection
    compile "com.google.dagger:dagger:${daggerVersion}"
    compile "com.google.dagger:dagger-android:${daggerVersion}"
    compile "com.google.dagger:dagger-android-support:${daggerVersion}"

    annotationProcessor "com.google.dagger:dagger-compiler:${daggerVersion}"

    // added to fix a compilation error migrating from dagger 2.10 to 2.11
    annotationProcessor 'com.google.guava:guava:22.0'

    // "provided" keyword refers to dependencies that are only needed at compilation
    provided 'javax.annotation:jsr250-api:1.0'

    // google spreadsheet api // https://developers.google.com/sheets/api/quickstart/android
    ////////////////////////////////////////////////////////////////////////////////////////

    // https://developers.google.com/android/guides/setup
    compile "com.google.android.gms:play-services-auth:11.0.1"

    // https://github.com/googlesamples/easypermissions
    compile 'pub.devrel:easypermissions:0.4.0'

    // https://developers.google.com/api-client-library/java/
    compile('com.google.api-client:google-api-client-android:1.22.0') {
        exclude group: 'org.apache.httpcomponents'
    }
    compile('com.google.apis:google-api-services-sheets:v4-rev476-1.22.0') {
        exclude group: 'org.apache.httpcomponents'
    }

    // LeakCanary: https://github.com/square/leakcanary
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5.1'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.1'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.1'


}
// firebase
apply plugin: 'com.google.gms.google-services'
